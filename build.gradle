apply plugin: 'java'
apply plugin: 'eclipse-wtp'

group = 'com.centurylink.mdwdemo'
version = '1.0.08'

description = "mdw-demo"

sourceCompatibility = 1.8
targetCompatibility = 1.8

eclipse {
    classpath {
        defaultOutputDir = file("build/classes")
        
        containers "GROOVY_SUPPORT"
        containers "GROOVY_DSL_SUPPORT"
        
        file {
            whenMerged { classpath ->
                // eclipse should process dynamic java, but should not be part of build
                def assetsSrc = new org.gradle.plugins.ide.eclipse.model.SourceFolder("assets", "build/assets")
                assetsSrc.excludes.add("Archive/")
                entries.add(assetsSrc)
                
                def groovySupport = entries.find { it.kind == "con" && it.path == "GROOVY_SUPPORT" }
                groovySupport.entryAttributes.put("minimal", "true")
            }
        }
    }
    wtp {
        facet {
            facet type: "fixed", name: "jst.web"
            facet name: "mdw.workflow", version: "5.1"
        }
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'assets'
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compile group: 'com.centurylink.mdw', name: 'mdw-common', version: mdwVersion
    compile group: 'com.centurylink.mdw', name: 'mdw-services', version: mdwVersion
    compile group: 'com.centurylink.mdw', name: 'mdw-workflow', version: mdwVersion
    compile group: "org.springframework", name: "spring-beans", version: springVersion
    compile group: "org.springframework", name: "spring-context", version: springVersion
    compile group: "org.springframework", name: "spring-core", version: springVersion

    // asset package dependencies
    compile fileTree(dir: "assets/com/centurylink/mdw/camel", includes: ["*.jar"])
    compile fileTree(dir: "assets/com/centurylink/mdw/drools", includes: ["*.jar"])
    compile fileTree(dir: "assets/com/centurylink/mdw/kafka", includes: ["*.jar"])
    compile fileTree(dir: "assets/com/centurylink/mdw/node", includes: ["*.jar"])
    compile group: "ch.vorburger.mariaDB4j", name: "mariaDB4j-core", version: "2.2.1"
    compile group: 'junit', name: 'junit', version:'4.8.1'
}

task copy(type: Copy) {
    def jarName = 'mdw-demo-' + version + '.jar'
    from "$buildDir/libs"
    into "${assetLoc}/com/centurylink/mdw/demo/cool"
    include jarName
    rename (jarName, 'mdw-demo.jar')
}

build  {
    dependsOn(copy)
}